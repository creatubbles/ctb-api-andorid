apply plugin: 'com.android.library'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'groovyx.android'
apply from: 'jacoco.gradle'

android {
    compileSdkVersion 25

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/services/org.codehaus.groovy.transform.ASTTransformation'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
    }
}

ext {
    bintrayRepo = 'maven'
    bintrayName = 'ctb-api-android/'

    publishedGroupId = 'com.creatubbles'
    libraryName = 'API-V2'
    artifact = 'api'

    libraryDescription = 'Creatubbles api v2'

    siteUrl = 'https://github.com/creatubbles/ctb-api-android'
    gitUrl = 'https://github.com/creatubbles/ctb-api-android.git'

    libraryVersion = "2.1." + System.getenv("CIRCLE_BUILD_NUM");

    developerId = 'p.szymanski'
    developerName = 'Pawel Szymanski'
    developerEmail = 'p.szymanski@nomtek.com'

    licenseName = 'MIT'
    licenseUrl = 'https://opensource.org/licenses/mit-license.php'
    allLicenses = ["MIT"]
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply plugin: 'com.jfrog.bintray'

version = libraryVersion

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    failOnError false
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Bintray
bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = false //Determines whether to GPG sign the files. The default is false
                //Optional. The passphrase for GPG signing'
                //passphrase = System.getenv("BINTRAY_GPG_PASSWORD")
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:support-annotations:25.2.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.4.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.4.0'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.google.dagger:dagger:2.7'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.7'
    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation 'com.pixplicity.easyprefs:library:1.8.1@aar'
    api 'com.github.creatubbles:java-jsonapi-converter:87dbdaf'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.8'
    testImplementation 'org.robolectric:robolectric:3.0'

    testImplementation "org.powermock:powermock-module-junit4:1.6.5"
    testImplementation "org.powermock:powermock-module-junit4-rule:1.6.5"
    testImplementation "org.powermock:powermock-api-mockito:1.6.5"
    testImplementation "org.powermock:powermock-classloading-xstream:1.6.5"

    testImplementation 'org.codehaus.groovy:groovy:2.4.7:grooid'
    testImplementation "com.andrewreitz:spock-android:1.2.1"
    testImplementation 'com.android.support.test:testing-support-lib:0.1'
    testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
        exclude group: 'junit'
    }
    testImplementation 'cglib:cglib-nodep:3.2.4'
    testImplementation 'org.objenesis:objenesis:2.2'
}
